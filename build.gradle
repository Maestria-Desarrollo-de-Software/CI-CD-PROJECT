plugins {
  id 'java'
  id 'checkstyle'
  id 'org.springframework.boot' version '3.4.1'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.graalvm.buildtools.native' version '0.11.3'
  id 'org.cyclonedx.bom' version '3.0.2'
  id 'io.spring.javaformat' version '0.0.47'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'com.github.spotbugs' version '6.0.26'
}

group = 'org.springframework.samples'
version = '4.0.0-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenCentral()
}

ext {
  webjarsLocatorLiteVersion = "1.1.2"
  webjarsFontawesomeVersion = "4.7.0"
  webjarsBootstrapVersion = "5.3.8"
}

gradle.startParameter.excludedTaskNames += ["checkFormatAot", "checkFormatAotTest"]

dependencies {
  // --- App ---
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  implementation 'javax.cache:cache-api'
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'

  runtimeOnly "org.webjars:webjars-locator-lite:${webjarsLocatorLiteVersion}"
  runtimeOnly "org.webjars.npm:bootstrap:${webjarsBootstrapVersion}"
  runtimeOnly "org.webjars.npm:font-awesome:${webjarsFontawesomeVersion}"

  runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'com.mysql:mysql-connector-j'
  runtimeOnly 'org.postgresql:postgresql'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.springframework.boot:spring-boot-docker-compose'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:mysql'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


tasks.named('test') {
  useJUnitPlatform()
}

/**
 * Spotless (formato) - quality gate con spotlessCheck
 */
spotless {
  java {
    googleJavaFormat('1.17.0')
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
    target 'src/**/*.java'
  }
}

/**
 * Checkstyle (reglas generales)
 */
checkstyle {
  toolVersion = '10.17.0'
  configDirectory = file('src/checkstyle')
  configFile = file('src/checkstyle/checkstyle.xml')
}

tasks.withType(Checkstyle).configureEach {
  reports {
    xml.required = false
    html.required = true
  }
}

/**
 * SpotBugs
 */
spotbugs {
  ignoreFailures = false
  effort.set(com.github.spotbugs.snom.Effort.valueOf("MAX"))
  reportLevel.set(com.github.spotbugs.snom.Confidence.valueOf("LOW"))
}

spotbugsMain {
  reports {
    html.required = true
    xml.required = false
  }
}

spotbugsTest {
  reports {
    html.required = true
    xml.required = false
  }
}

/**
 * Quality Gates
 */
tasks.named('check') {
  dependsOn 'spotlessCheck'
  dependsOn 'checkstyleMain', 'checkstyleTest'
  dependsOn 'spotbugsMain', 'spotbugsTest'
}

tasks.matching {
  it.name in [
    'checkstyleAot', 'checkstyleAotTest',
    'checkFormatAot', 'checkFormatAotTest',
    'formatAot', 'formatAotTest'
  ]
}.configureEach { enabled = false }

tasks.named("compileJava") {
  mustRunAfter("formatMain")
}

tasks.named("compileTestJava") {
  mustRunAfter("formatTest")
}

tasks.matching { it.name.toLowerCase().contains('spotbugsaot') }
  .configureEach { enabled = false }

tasks.matching { it.name in ['checkFormatMain', 'checkFormatTest', 'formatMain', 'formatTest'] }
  .configureEach { enabled = false }
